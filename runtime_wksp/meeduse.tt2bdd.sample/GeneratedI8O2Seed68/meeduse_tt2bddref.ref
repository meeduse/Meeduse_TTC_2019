REFINEMENT
	meeduse_tt2bddref
REFINES
	meeduse_tt2bddmain
INCLUDES
	meeduse_tt2bdd
DEFINITIONS
SET_PREF_COMPRESSION == TRUE;
	portRow(r) == (cellPort~ ; cells) |> r ;
	maxPort(p,r) == p : InputPort & r <: Row &
		not( # s . (s : InputPort & s /= p & s : dom(portRow(r))
			& card(portRow(r)[{s}]) > card(portRow(r)[{p}]))) ;
	zeroCells(p,r) == {c | c : Cell & cellPort(c) = p 
				& cells(c) : r & Cell_value(c) = FALSE} ;
	oneCells(p,r) == {c | c : Cell & cellPort(c) = p 
				& cells(c) : r & Cell_value(c) = TRUE} ;
	selectedCells == dom(Cell_selected |> {TRUE}) ;
	/*selectedRows == {r | r : Row & 
				not (
					# t . (t : Row & t /= r & 
					card(cells~[{r}] /\ selectedCells) < card(cells~[{t}] /\ selectedCells) 
					)
				)
			} ;*/
	selectedRows == LET cr BE cr = {c,r | r:Row & c= card(cells~[{r}] /\ selectedCells)} IN
	             LET mx BE mx = max(dom(cr)) IN
	               cr[{mx}]
	             END
	           END;
	outputCells(r) == {as | as : cells~[{r}] & cellPort(as) : OutputPort} ;
	inputCells(r) == {as | as : cells~[{r}] & cellPort(as) : InputPort}

VARIABLES
	branchOne, branchZero, 
	seqTree, selectedPorts, treePorts, seqLink
INVARIANT
	branchOne <: Tree &
	branchZero <: Tree &
	seqTree : seq(Tree) &
	selectedPorts <: Port &
	treePorts : InputPort <-> Tree &
	seqLink : seq(BOOL)
INITIALISATION
	branchOne := {} ||
	branchZero := {} ||
	seqTree := {} ||
	selectedPorts := {} ||
	treePorts := {} ||
	seqLink := {} ||
	setLastTree(card(Subtree))
OPERATIONS
TruthTable2BDD =
	ANY src WHERE
		src : TruthTable & src /: BDD
	THEN
		BDD_NEW(src) ;
		BddInput_NEW(InputPort) ;
		BddOutput_NEW(OutputPort)
	END;

Port2Port =
	ANY bdd WHERE
		bdd : BDD & bddPorts~[{bdd}] = {}
	THEN
		BDD_Addports(bdd, InputPort \/ OutputPort)
	END ;

SelectPort = 
	ANY port WHERE
		InputPort /= {}
		& port : InputPort /\ BddInput  
		& port /: cellPort[dom(Cell_selected |> {TRUE})]
		& maxPort(port, selectedRows)
		& ran(seqTree) /\ Leaf = {}
	THEN
		SELECT
			port : selectedPorts
		THEN
			seqTree := seqTree <- (treePorts(port))
		WHEN
			port /: selectedPorts
			& not(# portBis . (portBis /: cellPort[dom(Cell_selected |> {TRUE})] 
					& maxPort(portBis, selectedRows) 
					& portBis : selectedPorts))
		THEN	
			Subtree_NEW(port) ;
			selectedPorts := selectedPorts \/ {port} ;			
			treePorts(port) := lastTree ;
			seqTree := seqTree <- (lastTree) ;
			IF lastTree = 1 THEN
				Tree_SetOwnerBDD(lastTree, bddPorts(port))
			END
		END ;

		SELECT zeroCells(port, selectedRows) /= {} THEN
			Cells_SetSelected(zeroCells(port, selectedRows), TRUE) ;

			branchZero := branchZero \/ treePorts[{port}] ;

			seqLink := seqLink <- (FALSE)
		WHEN oneCells(port, selectedRows) /= {} THEN
			Cells_SetSelected(oneCells(port, selectedRows), TRUE) ;

			branchOne := branchOne \/ treePorts[{port}] ;
			
			seqLink := seqLink <- (TRUE)
		END
END;

setLinks =
PRE card(seqTree) > 1 THEN
	ANY t1, t2 WHERE 
		t1 = first(seqTree) & t2 = first(tail(seqTree))
		& ran(seqTree) /\ Leaf /= {} 
	THEN
		IF first(seqLink) = TRUE THEN
			Subtree_SetTreeForOne(t1, t2)
		ELSE
			Subtree_SetTreeForZero(t1, t2)
		END ;
		seqTree := tail(seqTree) ;
		seqLink := tail(seqLink)
	END
END;

Continue = 
	SELECT 
		card(seqTree) = 1 & ran(seqTree) /\ Leaf /= {}
	THEN
		seqTree := tail(seqTree) 
	END ;

Transform = 
	ANY row WHERE
		row : selectedRows
		& card(selectedRows) = 1
		& ! cc . (cc : cells~[{row}] & cellPort(cc) /: OutputPort => Cell_selected(cc) = TRUE)
	THEN
		IF card(outputCells(row)) > card(assignPort[outputCells(row)]) THEN
			ANY as WHERE as : outputCells(row) & assignOwner[{as}] = {} THEN
				Assignment_NEW(as, cellPort(as), Cell_value(as))
			END
		ELSE	
			Leaf_NEW ;
			seqTree := seqTree <- (lastTree) ;
			Assignments_SetOwner(outputCells(row), lastTree) ;
			Cells_Free(inputCells(row)\/ outputCells(row)) ;
			Row_Free(row) ;	
			selectedPorts := selectedPorts - {app | app : selectedPorts & treePorts(app) :  (branchZero /\ branchOne)}
		END	
	END
END


